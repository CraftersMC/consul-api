apply plugin: "java"
apply plugin: "idea"
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.google.code.gson:gson:2.8.9"
	implementation "org.apache.httpcomponents:httpcore:4.4.16"
	implementation "org.apache.httpcomponents:httpclient:4.5.14"

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'

	testImplementation "org.mockito:mockito-core:2.23.0"
	testImplementation "org.hamcrest:hamcrest-library:1.3"
	testImplementation "nl.jqno.equalsverifier:equalsverifier:3.4.1"
	testImplementation "org.slf4j:slf4j-simple:1.7.25"
	testImplementation "com.pszymczyk.consul:embedded-consul:1.0.2"
}

if (JavaVersion.current().isJava9Compatible()) {
	tasks.withType(JavaCompile).all { options.compilerArgs.addAll(['--release', '8']) }
}

test {
	enableAssertions = true
	useJUnitPlatform()

	testLogging {
		exceptionFormat 'full'
		events "PASSED", "STARTED", "FAILED", "SKIPPED"
		// Set to true if you want to see output from tests
		showStandardStreams false
	}
}

// --------------------------------------------------------------
// Tasks for publishing into Maven Central

tasks.register('sourcesJar', Jar) {
	from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
	from javadoc
}

artifacts {
	archives jar
	archives sourcesJar
	archives javadocJar
}

group = "com.ecwid.consul"
archivesBaseName = "consul-api"
version = "1.4.5c-SNAPSHOT"

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			name = "nexus"
			url = uri("https://repo.teon.llc/repository/maven-snapshots/")

			credentials {
				username = findProperty("nexusUsername") ?: System.getenv("NEXUS_USERNAME")
				password = findProperty("nexusPassword") ?: System.getenv("NEXUS_PASSWORD")
			}
		}
	}
}
